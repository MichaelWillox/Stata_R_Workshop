---
title: "R and Stata Workshop: Using Stata"
subtitle: |
 Economic and Finance Department \
 Brunel University London, UK
author: "Michael Willox"
date: "2024-06-09"
output: pdf_document
header-includes:
   - \usepackage{booktabs}
   - \usepackage{amsmath}
---

```{r setup, include=FALSE}
library(Statamarkdown)
library(knitr)
library(readr)
library(stargazer)
library(texreg)
opts_chunk$set(echo = TRUE)
```

## Setup

Use $mus08psidextract.dta$ to set up panel data in Stata, and define panel variable $id$, and time variable $t$.
Consider the following model:

$$lwage_{it} = \alpha + \beta_{1}exp_{it} + \beta_{2}exp2_{it} + \beta_{3}wks_{it} + \beta_{4}ed_{it} + \mu_{i} + \epsilon_{it}$$
Summarize and describe the dataset.

```{stata}
use "../Data/mus08psidextract.dta", clear
summarize
describe
```

## Questions 1. Determine if this panel data is the short or long panel.

Given that the intercept term in equation 1 of the assignment is common to all units, it suggests that the model being considered is characterized by random effects, which assumes the term $u_i$ is not correlated with the regressors, $X_{it}$.

Use the distinct command to count distinct values for the time and panel variables.
The dataset is short because $n = 595 > T = 7$. The total number of observations, $N$, is 4165.

```{stata}
use "../Data/mus08psidextract.dta", clear
distinct id t
```


## Questions 2. Run pooled OLS, fixed effects and random effects regressions?

Pooled OLS with a single intercept.
```{stata}
use "../Data/mus08psidextract.dta", clear
reg lwage exp exp2 wks ed
```

Fixed effects with unit specific intercepts and unit-specific, time-invariant error term that is uncorrelated with the explanatory variables. $u_{i}$ is assumed to be correlated with the regressors, $X_{it}$.

```{stata}
use "../Data/mus08psidextract.dta", clear
xtreg lwage exp exp2 wks ed, fe
```

Random effects with single intercept and a unit specific error term (random effect), which is uncorrelated with the explanatory variables, varies across units, is constant over time for each unit and is separate from the idiosyncratic error term.

```{stata}
use "../Data/mus08psidextract.dta", clear
xtreg lwage exp exp2 wks ed, re
```


## Questions 3. Does this model have multicollinearity or heteroscedasticity?

Pooled OLS exhibits multicollinearity.

```{stata}
use "../Data/mus08psidextract.dta", clear

reg lwage exp exp2 wks ed
estat vif
```

The *VIF* measures how much the variance of a regression coefficient is inflated due to multicollinearity with other predictors. A *VIF* value greater than 10 is often considered indicative of high multicollinearity, which can affect the stability and interpretation of the regression coefficients.

Although the mean *VIF* of 9.90 is just below 10, the individual *VIF* values for $exp$ and $exp^2$ are of more concern. Centering variables can help reduce multicollinearity. This involves subtracting the mean of a variable  from each of its values and then using this centered variable in the regression. Note that $estat vif$ does not work with $xtreg$ combined with the $fe$ or $re$ options. 

Here, the bar over the variable represents the centred or demeaned variable.

$$\overline{lwage_{it}} = \alpha + \beta_{1}\overline{exp_{it}} + \beta_{2}\overline{exp^2_{it}} + \beta_{3}\overline{wks_{it}} + \beta_{4}\overline{ed_{it}} + \mu_{i} + \epsilon_{it}$$


```{stata}
use "../Data/mus08psidextract.dta", clear

* Generate de-meaned variables
bys id: egen mean_lwage = mean(lwage)
bys id: egen mean_exp = mean(exp)
bys id: egen mean_exp2 = mean(exp2)
bys id: egen mean_wks = mean(wks)
bys id: egen mean_ed = mean(ed)

gen lwage_dm = lwage - mean_lwage
gen exp_dm = exp - mean_exp
gen exp2_dm = exp2 - mean_exp2
gen wks_dm = wks - mean_wks
gen ed_dm = ed - mean_ed

reg lwage_dm exp_dm exp2_dm wks_dm ed_dm

* Calculate VIFs
estat vif
```

```{stata}
use "../Data/mus08psidextract.dta", clear

reg lwage exp exp2 wks ed
estat hettest, iid
```
The result, *Prob > chi2 = 0.9763* indicates that the null hypothesis that the residuals are homoscedastic and cannot be rejected at standard levels of statistical significance. 


## Questions 4. Which method is suitable for this model, pooled OLS regression or a random effects model? 

```{stata}
use "../Data/mus08psidextract.dta", clear

reg lwage exp exp2 wks ed
estimates store OLS							

reg lwage exp exp2 wks ed, vce(cluster id)
estimates store OLSR		

xtreg lwage exp exp2 wks ed, fe 
estimates store FE		

xtreg lwage exp exp2 wks ed, re 
estimates store RE				

estimate table OLS OLSR	FE RE, star(.05 .01 .001) b(%7.2f)   
estimate table OLS OLSR	FE RE, b(%7.2f) se(%7.2f) p(%7.2f)	stats(N r2_a)
```

Based on the results, a random effects model appears to more suitable since the Wald chi-squared statistic (3012.45) with a p-value of 0.0000 indicates that the model is also highly significant. For the fixed effects model, the F-statistic (F(3, 3567) = 2273.74) with a p-value of 0.0000 indicates that the overall model is highly significant. Moreover, In the fixed effects model, $\rho$ = 0.9789, which indicates a high degree of correlation within groups (individuals). This suggests that there are individual-specific effects that need to be accounted for. Ignoring these effects in an OLS model (with or without robust standard errors) would lead to biased and inconsistent estimates.

The suitability of a fixed or random effects model depends on whether the regressors are correlated with the error term, which is addressed in the next question.

## Questions 5. Compare the random effect and the fixed effect model, which one is better? 

```{stata}
use "../Data/mus08psidextract.dta", clear

xtreg lwage exp exp2 wks ed, fe
estimates store FE		

xtreg lwage exp exp2 wks ed, re
estimates store RE	

hausman FE RE
```

Based on the $\chi^2$ = 6191.43 and p-value = 0.0000, we can reject the null hypothesis that there is no correlation between the regressors and the error. This implies that,

$$\mathbb{E}[\epsilon_{it} \mid X_{i1}, X_{i2}, \ldots] \neq 0.$$

## Questions 6. Export the above regression results to Excel, Word or Latex. (only need to output one).

The *outreg2* produces nicely formatted MS Word documents. However, *outreg2* produces csv files, they are not well formatted. Latex and text files look better, but they are difficult to read back into R Markdown and render in a pdf as a nicely formatted table. The best alternative is to save a table of regression coefficients using *putexcel*. The Excel file can then be read into R Markdown to display the results in the rendered pdf. However, some effort would be needed to format the table nicely.

Consider another model:

$$lwage_{it} = \alpha + \beta_{1}exp_{it} + \beta_{2}exp^2_{it} + \beta_{3}wks_{it} + \beta_{4}ed_{it} + \beta_{5}occ_{it} + \epsilon_{it}$$
```{stata}
use "../Data/mus08psidextract.dta", clear
cd "C:/Users/micha/MyDocuments/Brunel/Stata_R_Workshop/Programs"
quietly regress lwage exp exp2 wks occ ed
estimates store OLS		

outreg2 using myreg.doc,replace ctitle (OLS)

quietly regress lwage exp exp2 wks occ ed
eret li

matrix coef = r(table)
mat li coef
quietly putexcel set regress.xlsx, replace

putexcel A1 = matrix(coef), names
quietly putexcel save 

estimate table OLS, star(.05 .01 .001) b(%7.2f)	stats(N r2_a)  
```
Here is the raw table of regression output after reading in the Excel file created in the previous step.

```{r}
x <- readxl::read_xlsx("C:/Users/micha/MyDocuments/Brunel/Stata_R_Workshop/Programs/regress.xlsx")
names(x)[1] <- "variable"   # the first column needs a name
unlink("regress.xlsx")      # cleanup the output file
x
```

## Questions 7. Consider an endogenous variable $\beta_{5}occ_{it}$, and $south_{it}$ and $fem_{it}$ as instrumental variables. 

Since wages likely influence occupation, there is a high probability that $occ$ is endogenous and, therefore, correlated with the error term.

```{stata}
use "../Data/mus08psidextract.dta", clear

* First-stage regression
regress occ south fem exp exp2 wks ed
```

In the first-stage regression the endogenous variable $occ$ is regressed on the instruments $south$ and $fem$, along with any other exogenous variables. The coefficients of $south$ and $fem$ are relatively large compared to the coefficients for the other regressors and they statistically significant, which indicates that the instruments are correlated with the endogenous variable.

The F-statistic for the joint significance of the instruments ($south$ and $fem$) and other exogenous variables is large and statistically significant. It is also worth noting that the R-squared value of the first-stage regression is 0.3966, which suggests that the instruments and other exogenous variables explain a substantial amount of the variation in $occ$.


## Questions 8. Run 2SLS and GMM. 

```{stata}
use "../Data/mus08psidextract.dta", clear

* 2SLS
ivregress 2sls lwage exp exp2 wks ed (occ = south fem)

* GMM
gmm (lwage - {b0} - {b1}*exp - {b2}*exp2 - {b3}*wks - {b4}*ed - {b5}*occ), ///
    instruments(exp exp2 wks ed south fem) 

* IVGMM
ivregress gmm lwage exp exp2 wks ed (occ = south fem), vce(robust)
```

The results for GMM and IVGMM are identical. The results for the 2SLS are nearly identical to the results from the GMM and IVGMM.


## Questions 9. Test if $occ_{it}$ is endogenous or not, and examine $south_{it}$, $fem_{it}$ are valid instrumental variables. 

```{stata}
use "../Data/mus08psidextract.dta", clear

regress lwage exp exp2 wks occ ed
estimates store ols	

ivregress 2sls lwage exp exp2 wks ed (occ = south fem)
estimates store iv
hausman iv ols,constant

estat overid
estat endogenous occ
```

The Hausman test compares the estimates from the OLS model with those from an IV model. There are significant differences between these estimates suggesting that the OLS suffers from endogeneity due to $occ$.

## Questions 10. Store OLS, 2SLS and GMM regression results in Stata. 


```{stata} 
use "../Data/mus08psidextract.dta", clear

* OLS
regress lwage exp exp2 wks occ ed
estimate store OLS

* 2SLS
ivregress 2sls lwage exp exp2 wks ed (occ = south fem)
estimate store TSLS

* GMM
gmm (lwage - {b0} - {b1}*exp - {b2}*exp2 - {b3}*wks - {b4}*ed - {b5}*occ), ///
    instruments(exp exp2 wks ed south fem) 
estimate store GMM

* IVGMM
ivregress gmm lwage exp exp2 wks ed (occ = south fem), vce(robust)
estimate store IVGMM

estimate table OLS TSLS GMM IVGMM, star(.05 .01 .001) b(%7.2f) stats(N r2_a)
estimate table OLS TSLS GMM IVGMM, b(%7.2f) se(%7.2f) p(%7.2f)	stats(N r2_a)
```